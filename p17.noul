day := 17;
import "prelude.noul";
puzzle_input := advent_input().strip;
# puzzle_input := "ihgpwlah";

start := [V(0, 0), ""];

transitions := \(loc, hist) -> (
	h := hex_encode(md5(utf8encode(puzzle_input $ hist)));
	for (ix, dir, delta <- [
		[0, "U", V(-1, 0)],
		[1, "D", V(1, 0)],
		[2, "L", V(0, -1)],
		[3, "R", V(0, 1)]];
		if h[ix] in "bcdef";
		if loc + delta all (0 <= _ <= 3)
	) yield [loc + delta, hist $ dir]
);

seen := {start};
batch := [start];
next_batch := [];
longest := "";
while (batch) (
	for (st <- batch; t <- transitions(st); if t not_in seen) (
		if (t[0] == V(3, 3)) (
			# submit! 1, t[1];
			# submit! 1, t[1];
			if (len(t[1]) > len(longest)) longest = t[1];
		) else (
			seen[t] = st;
			next_batch append= t;
		)
	);
	batch = next_batch;
	next_batch = [];
);
submit! 2, len(longest)
